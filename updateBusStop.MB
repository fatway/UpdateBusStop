'*****************************************************************************
'       Copyright (c) 2012, Guangdong HYLT Co.,Ltd.
'       All rights reserved.
'
' $Workfile: updateBusStop.MB $
' $Revision: 0.5 $
' $Author: Lee $
' $Date: 2012/9/14 $
'
' 工具描述:
'
'     合并散列的公交站点为同一个站点
'     更新所有点到统一的几何中心位置 & 更新统一的新ID & 设置样式
'
' 历史版本:
'
'    Rev 0.1  2012/8/15
'             实现基本的读取字段信息和遍历点位功能
'    Rev 0.2  2012/8/16
'             实现添加按钮功能
'             解决读取和更新点位XY坐标的功能
'    Rev 0.3  2012/8/17
'             实现变更点位样式功能
'    Rev 0.4  2012/8/28
'             判断表Selection是否存在 避免程序崩溃
'             添加shift-h为合并热键
'             合并完成后清除全部选中站点
'    Rev 0.5  2012/9/14
'             为方便操作工具条中增加现有的选择和平移功能
'             修改热键方便操作
'
'*****************************************************************************

Include "mapbasic.def"
Include "icons.def"

Declare Sub Main
Declare Sub UnionBusPoint
Declare Sub ToolInfo
Declare Sub Test


'创建按钮的主函数
Sub Main
    '增加合并公交的按键
    Create ButtonPad "公交" As
        PushButton
            Calling UnionBusPoint
            HelpMsg "UnionBusPoint\n合并公交站点"
	        Icon MI_ICON_TRANSPORT_13
        PushButton
            Calling ToolInfo
            HelpMsg "ToolInfo\n工具信息"
            Icon MI_ICON_MISC_4
        Show

    '增加合并公交的菜单（主要为了添加快捷键）
    Create Menu "华业公交数据处理[&B]" As
        "合并站点[&H]" + "\tF12/W%123/XF12" Calling UnionBusPoint,
        '/W#Z就是快捷键为SHIFT+Z
        '/W@Z就是快捷键为ALT+Z
        '/W^Z就是快捷键为CRTL+Z
        "(-",
        "信息[&I]" Calling ToolInfo,
        "(-",
        "快速单点选择" + "\tF9/W%120/XF9" Calling 1701,
        "快速矩形选择" + "\tF10/W%121/XF10" Calling 1722,
        "快速地图漫游" + "\tF11/W%122/XF11" Calling 1702
    Alter Menu Bar Add "华业公交数据处理[&B]"

End Sub



'合并公交站点函数
Sub UnionBusPoint
    Dim counter As Integer    '选中点位计数
    Dim xc, yc As Float       '取坐标值
    Dim uid As String         '取一个ID值作为合并后的新ID
    Dim _obj As Object        '定义一个空间对象（用于遍历点位）
    Dim mysymb As Symbol      '合并后修改样式

    counter = 0
    xc = 0.0
    yc = 0.0
    mysymb = MakeSymbol(40,65280,12)

    
    GoTo ComeOn               '添加错误捕获，解决没有选择点直接合并导致的崩溃


    '表Selection不存在时退出
NoSelect:
    Exit Sub


ComeOn:
    '捕获没有选中点位的异常
    OnError GoTo NoSelect

    '遍历点，获取点数、坐标中心、最后一个ID值信息
    Fetch First From Selection
    Do While Not EOT(Selection)
        uid = Selection.站点ID                         '''HACK: （昆明公交中ID字段为NAME）

        xc = xc + CentroidX(Selection.obj)           '累加坐标值
        yc = yc + CentroidY(Selection.obj)

        counter = counter + 1
        Fetch Next From Selection
    Loop

    '计算出几何中心坐标值
    xc = xc / counter
    yc = yc / counter


    '二次遍历点，更新所有点到几何中心并变更样式
    Fetch First From Selection
    Do While Not EOT(Selection)
	    _obj = Selection.obj
	
        '设置新坐标
        Alter Object _obj Geography OBJ_GEO_POINTX, xc
        Alter Object _obj Geography OBJ_GEO_POINTY, yc
        
	    '设置新样式
        Alter Object _obj Info OBJ_INFO_SYMBOL, mysymb

        '更新点状态到实际数据中
        Update Selection Set obj = _obj

        Fetch Next From Selection
    Loop

    
    '统一赋值新的ID
    Update Selection Set newid = uid                '''HACK: （昆明公交中合并ID字段为NewID）


    '结束通知
    Note Str$(counter) + "个点位被更新了 ^_^"

    '清除选中要素
    Select * From Selection Where 1=2

End Sub



'作者信息函数
Sub ToolInfo
    Note "公交站点合并工具" & Chr$(10) &
         "对选中要素赋值统一的新 [坐标] & [样式] & [NewGUID]" & Chr$(10) &
         Chr$(10) &
         "[坐标]:" & Chr$(9) & "所有选中点位的几何中心" & Chr$(10) &
         "[样式]:" & Chr$(9) & "区别于原有点位样式" & Chr$(10) &
         "[NewGUID]: 最后一个选中点的GUID" & Chr$(10) &
         Chr$(10) &
         "李晓平 @ 技术支撑中心"
End Sub



'测试函数
Sub Test
    Note NumTables()
    'Update Selection Set obj=CreatePoint(xc, yc)
End Sub
